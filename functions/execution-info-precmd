# vim:et sts=2 sw=2 ft=zsh
builtin emulate -L zsh

if (( __EXECUTION_INFO_PREEXEC_REALTIME )); then
  local precmd_realtime=${EPOCHREALTIME}
  local -rF duration_realtime=$(( precmd_realtime - __EXECUTION_INFO_PREEXEC_REALTIME ))
  local -ri preexec_timestamp=${__EXECUTION_INFO_PREEXEC_REALTIME}
  local -ri precmd_timestamp=${precmd_realtime}
  unset __EXECUTION_INFO_PREEXEC_REALTIME

  # Start
  local start_format
  zstyle -s ':zim:execution-info' start-format 'start_format' || start_format="%Y-%m-%d %H:%M:%S"
  if date --version >/dev/null 2>&1; then
    # GNU date
    execution_start_info=$(date -d "@${preexec_timestamp}" +"${start_format}")
  else
    # BSD date
    execution_start_info=$(date -r "${preexec_timestamp}" +"${start_format}")
  fi

  # End
  local end_format
  zstyle -s ':zim:execution-info' end-format 'end_format' || end_format="%Y-%m-%d %H:%M:%S"
  if date --version >/dev/null 2>&1; then
    # GNU date
    execution_end_info=$(date -d "@${precmd_timestamp}" +"${end_format}")
  else
    # BSD date
    execution_end_info=$(date -r "${precmd_timestamp}" +"${end_format}")
  fi

  # Duration
  local -F threshold_realtime
  zstyle -s ':zim:execution-info' duration-threshold 'threshold_realtime' || threshold_realtime=2
  local duration_format
  zstyle -s ':zim:execution-info' duration-format 'duration_format' || duration_format="%d"
  if (( duration_realtime >= threshold_realtime )); then
    local -ri ms=$(( duration_realtime%1*1000 ))
    local -ri s=$(( duration_realtime%60 ))
    local -ri m=$(( (duration_realtime/60)%60 ))
    local -ri h=$(( duration_realtime/3600 ))
    local duration
    if (( h > 0 )); then
      duration="${h}h ${m}m"
    elif (( m > 0 )); then
      duration="${m}m ${s}s"
    elif (( s > 0 )); then
      duration="${s}s ${ms}ms"
    else
      duration="${ms}ms"
    fi
    zformat -f execution_duration_info ${duration_format} d:${duration}
  else
    # Don't show duration
    unset execution_duration_info
  fi
else
  # Clear previous when hitting ENTER with no command to execute
  unset execution_start_info
  unset execution_end_info
  unset execution_duration_info
fi
